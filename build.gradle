plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.23'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'application'
    id 'antlr'
}

mainClassName = 'net.ninjacat.rowcp.AppKt'

group 'net.ninjacat.rowcp'
version '0.6.2'

repositories {
    mavenCentral()
}

ext {
    antlrVersion = '4.13.1'
    jansiVersion = '2.4.1'
    jcommanderVersion = '1.82'
    mariaDbVersion = '3.3.3'
    postgresVersion = '42.7.3'
    h2Version = '1.4.200'
    mysqlVersion = '8.0.33'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.23'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'

    antlr "org.antlr:antlr4:$antlrVersion"
    implementation "org.antlr:antlr4-runtime:$antlrVersion"

    implementation "org.fusesource.jansi:jansi:$jansiVersion"
    implementation "com.beust:jcommander:$jcommanderVersion"

    implementation "org.mariadb.jdbc:mariadb-java-client:$mariaDbVersion"
    implementation "org.postgresql:postgresql:$postgresVersion"
    implementation "com.h2database:h2:$h2Version"
    implementation "mysql:mysql-connector-java:$mysqlVersion"

    implementation 'com.amazonaws.secretsmanager:aws-secretsmanager-jdbc:2.0.2'

    implementation 'ch.qos.logback:logback-classic:1.5.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'org.liquibase:liquibase-core:4.27.0'
    testImplementation 'io.mockk:mockk:1.13.10'
}

generateGrammarSource {
    arguments += ["-visitor"]
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava.dependsOn generateGrammarSource

compileKotlin {
    dependsOn generateGrammarSource
    kotlinOptions {
        jvmTarget = "17"
    }
}

compileTestKotlin.dependsOn generateTestGrammarSource

test {
    useJUnitPlatform()
}

jar {
    archiveClassifier.set('non-bin')
}

shadowJar {
    dependsOn jar
    archiveClassifier.set('')
}
